# Maintainer: Tim Stahlhut <stahta01@gmail.com>
# Contributor: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Martell Malone <martellmalone@gmail.com>

_realname=headers
_mingw_suff=mingw-w64-cross
pkgbase="${_mingw_suff}-${_realname}"
pkgname=("${_mingw_suff}-${_realname}")
pkgdesc="MinGW-w64 headers for cross-compiler"
pkgver=7.0.0
pkgrel=1
arch=('i686' 'x86_64')
url="https://mingw-w64.sourceforge.io/"
license=('custom')
provides=("${_mingw_suff}-${_realname}=${pkgver}")
groups=("${_mingw_suff}-toolchain" "${_mingw_suff}")
makedepends=()
options=('!strip' '!libtool' '!emptydirs' '!buildflags')
_sourcedir=mingw-w64-v${pkgver}
source=("https://sourceforge.net/projects/mingw-w64/files/mingw-w64/mingw-w64-release/mingw-w64-v${pkgver}.tar.bz2")
sha256sums=('SKIP')

_targets="i686-w64-mingw32 x86_64-w64-mingw32 armv7-w64-mingw32"

prepare() {
  cd "${srcdir}/${_sourcedir}"
}

build() {
  for _target in ${_targets}; do
    msg "Configuring ${_target} headers"
    mkdir -p ${srcdir}/headers-${_target} && cd ${srcdir}/headers-${_target}
    ${srcdir}/${_sourcedir}/mingw-w64-headers/configure \
    --build=${CHOST} \
    --host=${_target} \
    --prefix=/opt/${_target} \
    --enable-sdk=all \
    --enable-secure-api \
    --with-default-win32-winnt=0x601 \
    --enable-idl \
    --without-widl
  done
}

package() {
  for _target in ${_targets}; do
    msg "Installing ${_target} headers"
    cd ${srcdir}/headers-${_target}
    make DESTDIR=${pkgdir} install

    # Moved these statements to mingw-w64-cross-winpthreads
    #   because mingw-w64-cross-crt needed them
    #rm ${pkgdir}/opt/${_target}/include/pthread_signal.h
    #rm ${pkgdir}/opt/${_target}/include/pthread_time.h
    #rm ${pkgdir}/opt/${_target}/include/pthread_unistd.h
  done
}
