# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Martell Malone <martellmalone@gmail.com>

_realname=binutils
_mingw_suff=mingw-w64-cross
pkgname=("${_mingw_suff}-${_realname}")
#_basever=2.24.51
#pkgver=${_basever}.20131119
pkgver=2.28
pkgrel=1
pkgdesc="A set of programs to assemble and manipulate binary and object files"
arch=('i686' 'x86_64')
url="https://www.gnu.org/software/binutils/"
license=('GPL')
groups=("${_mingw_suff}-toolchain" "${_mingw_suff}")
depends=("libiconv" "zlib")
checkdepends=('dejagnu' 'bc')
makedepends=("libiconv" "zlib")
options=('staticlibs' '!distcc' '!ccache' '!buildflags')
#install=binutils.install
source=(#"https://mirrors.kernel.org/sources.redhat.com/binutils/snapshots/binutils-${_basever}.tar.bz2"
        https://ftp.gnu.org/gnu/binutils/binutils-${pkgver}.tar.bz2{,.sig}
        0001-enable-gold-on.mingw32.patch
        0002-check-for-unusual-file-harder.patch
        0007-armv7-w64-mingw32.patch
        0008-fix-libiberty-makefile.mingw.patch
        0009-fix-libiberty-configure.mingw.patch
        0010-bfd-Increase-_bfd_coff_max_nscns-to-65279.patch
        #0900-binutils-trunk-msys2.patch
        0100-binutils-2.28-msys2.patch
        0110-binutils-mingw-gnu-print.patch)
sha256sums=('6297433ee120b11b4b0a1c8f3512d7d73501753142ab9e2daa13c5a3edd32a72'
            'SKIP'
            '1455bb98abfab4b04cce78671da65046718029c21e952a2e86384735a7e61ac8'
            '22c45e760b52b8169a1aef63ab8beea1913a1a706c1223a9c986f9075108920f'
            'fa634d3f1827051a3833fed8aed4bda8cb5d5ba46ba08829ebe97c4cceae624f'
            '20151c50ec91718f07af768f77a6880dbdcc497f1116a4dd73143303ec5f972d'
            'ad2e39ad43be3022c674cd0e0cc62ffeb2b3bb80f4f020bc96dbd6215b03764f'
            '8c86e28c9073a79bd40677d4c0447aa2b5b87c6da7bec5576b85823736f687cf'
            #'4661ea9608e0c00c95dff5a43e4db69917a16b2877f4fae2e4f4330a5ec592be'
            'b072622977e44e57192b3ee6471092d49e407e88a4f56c95833c1f4d0f1dfefb'
            '1b7ca45e74bb6e3addccfaba67cae94be0a0f78f632438869c12c70c8afa39f4')

_targets="i686-w64-mingw32 x86_64-w64-mingw32" # armv7-w64-mingw32

prepare() {
  cd ${srcdir}/binutils-${pkgver}

  patch -p1 -i "${srcdir}"/0001-enable-gold-on.mingw32.patch
  patch -p1 -i "${srcdir}"/0002-check-for-unusual-file-harder.patch
  #patch -p1 -i "${srcdir}"/0007-armv7-w64-mingw32.patch
  patch -p1 -i "${srcdir}"/0008-fix-libiberty-makefile.mingw.patch
  patch -p1 -i "${srcdir}"/0009-fix-libiberty-configure.mingw.patch
  patch -p1 -i "${srcdir}"/0010-bfd-Increase-_bfd_coff_max_nscns-to-65279.patch
  
  # Upstream patches
  # patch -p1 -i "${srcdir}"/27aaeda.diff

  patch -p1 -i "${srcdir}"/0110-binutils-mingw-gnu-print.patch
  #patch -p1 -i "${srcdir}"/0900-binutils-trunk-msys2.patch
  patch -p1 -i "${srcdir}"/0100-binutils-2.28-msys2.patch

  #do not install libiberty
  #sed -i 's/install_to_$(INSTALL_DEST) //' libiberty/Makefile.in
  # hack! - libiberty configure tests for header files using "$CPP $CPPFLAGS"
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure

}

build() {
  for _target in ${_targets}; do
    mkdir -p ${srcdir}/binutils-build-${_target} && cd ${srcdir}/binutils-build-${_target}

    if [ "${_target}" != "armv7-w64-mingw32" ]; then
      local _conf='--enable-lto'
    else
      local _conf=''
    fi

    if [ "${_target}" = "x86_64-w64-mingw32" ]; then
     _conf+=' --enable-64-bit-bfd'
    fi

    ${srcdir}/binutils-${pkgver}/configure \
      --build=${CHOST} \
      --host=${CHOST} \
      --target=${_target} \
      --prefix=/opt \
      --disable-werror \
      --with-libiconv-prefix=/usr \
      $_conf

    make
  done
}

check() {
  for _target in ${_targets}; do
    cd ${srcdir}/binutils-build-${_target}

    # unset LDFLAGS as testsuite makes assumptions about which ones are active
    # do not abort on errors - manually check log files
    make LDFLAGS="" -k check || true
  done
}

package() {
  for _target in ${_targets}; do
    cd ${srcdir}/binutils-build-${_target}
    make DESTDIR=${pkgdir} install

    # Remove unwanted files
    #rm ${destdir}/usr/share/info/{configure,standards}.info

    # Add some useful headers
    #install -m644 ${srcdir}/binutils-${pkgver}/include/libiberty.h ${pkgdir}/opt/include
    #install -m644 ${srcdir}/binutils-${pkgver}/include/demangle.h ${pkgdir}/opt/include
  done
}
